buildscript {
    ext {
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.moowork.gradle:gradle-node-plugin:1.1.1"

        // https://github.com/dorongold/gradle-task-tree
        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.3"
    }
}


apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'

// prints task dependency tree report to the console: gradlew tastkTree build, gradlew tastkTree bootRun
// https://github.com/dorongold/gradle-task-tree
apply plugin: "com.dorongold.task-tree"

// enable building wars: gradlew BootWar
apply plugin: 'war'

// fuilding frontend with npm
// https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md
apply plugin: "com.moowork.node"

// add our development build NpmTask named buildClientDev
// from com.moowork.node
task buildClientDev(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'build'
    description = 'Compile client side folder for development'
    args = ['run', 'buildDev']
}

// from com.moowork.node
task buildClient(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'build'
    description = "Compile client side folder for production"
    args = ['run', 'build']
}

// setup watcher on this ng build to link to our overall java development build.
// from com.moowork.node
task buildClientWatch(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'application'
    description = "Build and watches the client side assets for rebuilding"
    args = ['run', 'buildWatch']
}

// here we define, that task "buildClientDev" must be triggered on "gradlew bootrun"
bootRun.dependsOn(buildClientDev)

// here we define, that task "buildClient" must be triggered on "gradlew build", which creates a fat jar
jar.dependsOn(buildClient)


group = 'de.webapp.spring'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
    maven { url 'https://repo.spring.io/libs-snapshot' }
}


dependencies {
    // makes the web application startable
    compile("org.springframework.boot:spring-boot-starter")
    compile("org.springframework.boot:spring-boot-starter-web")
    testCompile('org.springframework.boot:spring-boot-starter-test')

    //data
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-data-rest")

    // RepositoryRestConfigurerAdapter
    compile "org.springframework.data:spring-data-rest-core"
    compile "org.springframework.data:spring-data-rest-webmvc"
    compile "org.springframework:spring-context"

    // enables HAL browser
    compile "org.springframework.data:spring-data-rest-hal-browser"

    // entity requirements
    compile "com.h2database:h2:1.4.193"
    compile "javax.xml.bind:jaxb-api"
}
